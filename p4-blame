#!/usr/bin/env perl
# p4-blame --- extended p4 annotate
# Author: Noah Friedman <friedman@splode.com>
# Created: 2013-02-26
# Public domain

# $Id: p4-blame,v 1.1 2013/02/26 19:30:14 friedman Exp $

# Commentary:
# Code:

use FindBin;
use lib "$FindBin::Bin/../lib/perl";
use lib "$ENV{HOME}/lib/perl";

use strict;
use NF::P4cmd qw(:direct);

sub log10 { log ($_[0]) / log (10) }

sub main
{
  my @data = p4 (qw(annotate -c -db -dw -dl), @_);
  my %change;

  my $line = 1;
  my $linewidth = 1 + int (log10 (scalar @data));
  my $fmt = sprintf ("%%6d  %%s  %%-9s  %%%dd: %%s", $linewidth);

  for my $elt (@data)
    {
      my $changeno = $elt->{lower};
      next unless defined $changeno;

      my $info = $change{$changeno};
      unless (defined $info)
        {
          my @desc = p4 (qw(change -o), $changeno);
          $info = $desc[0];
          $info->{Date} = substr ($info->{Date}, 0, 10);
          $change{$changeno} = $info;
        }
      printf($fmt, $changeno, $info->{Date}, $info->{User}, $line++, $elt->{data});
    }
}

main (@ARGV);

# eof
